apply plugin: 'com.android.application'
apply plugin: 'de.timfreiheit.buildtypelaunchericon.plugin'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    useLibrary 'org.apache.http.legacy'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'

        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'

        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    defaultConfig {
        applicationId "org.wheelmap.android.online"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion 15
        targetSdkVersion 22
        versionName "3.0.2"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    dataBinding {
        enabled true
    }

    buildTypes {

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            buildConfigField "String", "HOCKEY_APP_ID", "null"
            buildConfigField "String", "API_BASE_URL", "\"https://staging.wheelmap.org\""
            buildConfigField "String", "WHEELMAP_API_KEY", WHEELMAP_STAGING_API_KEY
            buildConfigField "String", "MAPBOX_API_KEY", MAPBOX_API_KEY
            buildConfigField "String", "ANALYTICS_TRACKING_ID", "\"\""
        }

        alpha {
            initWith(buildTypes.debug)
            signingConfig android.signingConfigs.debug
            applicationIdSuffix ".stagingdev"
            buildConfigField "String", "HOCKEY_APP_ID", "\"d076b4be1cf6f7d5130c4d766b6c7753\""
            buildConfigField "String", "ANALYTICS_TRACKING_ID", "\"\""
        }

        beta {
            initWith(buildTypes.debug)
            signingConfig android.signingConfigs.debug
            applicationIdSuffix ".staging"
            buildConfigField "String", "HOCKEY_APP_ID", "\"53b3b9ffa1473a9536e860791cfaa0b3\""
            buildConfigField "String", "ANALYTICS_TRACKING_ID", ANALYTICS_TRACKING_ID_DEVELOPMENT
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "HOCKEY_APP_ID", "\"1ede13fa382830c25625ec55f416ad06\""
            buildConfigField "String", "API_BASE_URL", "\"https://wheelmap.org\""
            buildConfigField "String", "WHEELMAP_API_KEY", WHEELMAP_API_KEY
            buildConfigField "String", "MAPBOX_API_KEY", MAPBOX_API_KEY
            buildConfigField "String", "ANALYTICS_TRACKING_ID", ANALYTICS_TRACKING_ID_RELEASE
        }

        releaseTest {
            initWith(buildTypes.debug)
            debuggable true
            applicationIdSuffix ".releaseTest"
            buildConfigField "String", "API_BASE_URL", "\"https://wheelmap.org\""
            buildConfigField "String", "WHEELMAP_API_KEY", WHEELMAP_API_KEY
            buildConfigField "String", "MAPBOX_API_KEY", MAPBOX_API_KEY
            buildConfigField "String", "HOCKEY_APP_ID", "\"ad5bad6dac5749cc9fc1aea17c71552d\""
            buildConfigField "String", "ANALYTICS_TRACKING_ID", ANALYTICS_TRACKING_ID_RELEASE
        }
    }
}

buildTypeLauncherIcon {
    mipmap = false
}

repositories {

    flatDir {
        dirs 'libs'
    }

    flatDir {
        dirs '../libs_tango/aar'
    }

}

dependencies {

    compile 'com.android.support:multidex:1.0.1'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    def android_support_version = "24.2.0"
    compile "com.android.support:appcompat-v7:${android_support_version}"
    androidTestCompile "com.android.support:appcompat-v7:${android_support_version}"
    compile "com.android.support:design:${android_support_version}"
    compile "com.android.support:cardview-v7:${android_support_version}"
    compile "com.android.support:transition:${android_support_version}"

    compile 'net.hockeyapp.android:HockeySDK:3.5.0'

    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'

    // http://mvnrepository.com/artifact/com.nostra13.universalimageloader/universal-image-loader/1.9.4
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'

    compile 'org.roboguice:roboguice:2.0'

    compile "org.apache.httpcomponents:httpmime:4.2.3"

    compile 'org.osmdroid:osmdroid-android:5.2'
    compile 'org.slf4j:slf4j-simple:1.6.1'

    compile 'de.akquinet.android.androlog:androlog:1.0.6'

    compile 'de.greenrobot:eventbus:2.4.0'

    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'

    compile 'com.nineoldandroids:library:2.4.0'

    compile 'com.facebook.stetho:stetho:1.3.0'

    // https://github.com/hotchemi/Android-Rate
    compile 'com.github.hotchemi:android-rate:0.5.6'

    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        // exclusion is not necessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }

    // Tango:
    compile 'org.rajawali3d:rajawali:1.0.325@aar'
    compile(name: 'tango_support_java_lib', ext: 'aar')
    compile(name: 'tango-ux-support-library', ext: 'aar')
    compile project(':tango_utils')

    // AutoValue
    def auto_value_version = "1.2"
    apt "com.google.auto.value:auto-value:${auto_value_version}"
    provided 'com.jakewharton.auto.value:auto-value-annotations:1.2-update1'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.3-rc1'
    apt 'com.ryanharter.auto.value:auto-value-gson:0.4.0'
    provided 'com.ryanharter.auto.value:auto-value-gson:0.4.0'

    compile 'com.google.android.gms:play-services-analytics:7.8.0'

    // Testing-only dependencies

    androidTestCompile('com.jayway.awaitility:awaitility:1.3.4'){
        exclude module : 'cglib-nodep'
        exclude group: 'com.hamcrest', module: 'hamcrest-core'
        exclude group: 'com.hamcrest', module: 'hamcrest-library'
    }
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2'

}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500"
    }
}
